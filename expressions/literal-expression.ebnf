(* Grammar of a literal expression *)

literal_expression = literal
                   | array_literal | dictionary_literal | playground_literal
                   | "#file" | "#fileID" | "#filePath"
                   | "#line" | "#column" | "#function" | "#dsohandle" ;
array_literal = "[" [ array_literal_items ] "]" ;
array_literal_items = array_literal_item [ "," ] | array_literal_item "," array_literal_items ;
array_literal_item = expression ;
dictionary_literal = "[" dictionary_literal_items "]" | "[" ":" "]" ;
dictionary_literal_items = dictionary_literal_item [ "," ] | dictionary_literal_item "," dictionary_literal_items ;
dictionary_literal_item = expression ":" expression ;
playground_literal = "#colorLiteral" "(" "red" ":" expression "," "green" ":" expression "," "blue" ":" expression "," "alpha" ":" expression ")"
                   | "#fileLiteral" "(" "resourceName" ":" expression ")"
                   | "#imageLiteral" "(" "resourceName" ":" expression ")" ;
